#!/usr/bin/sed -nf

# удаление всех (начальных) пустых строк
/./!d

# теперь у нас имеется непустая строка
:x
	# печатаем её
	p
	# извлекаем следующую
	n
	# есть символы? тогда продолжаем печатать непустые строки
	/./bx

# символов нет - эта строка пустая
:z
	# вынимаем следующую строку, если её нет (текущая - последняя)
	# во время выполнения n скрипт завершает работу
	n
	# ещё одна пустая? тогда мы её игнорируем, и переходим к следующей
	# этот скрипт удаляет ВСЕ пустые строки
	/./!bz

# все пустые строки були удалены/проигнорированы
# и теперь мы добавляем ещё одну пустую строку ПЕРЕД той, что сейчас напечатается
i\

bx

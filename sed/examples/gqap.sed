#!/bin/sed -rf

:begin_loop
/.{40}/! {
# если у нас ещё меньше чем 40 символов, то мы загрузим
# ещё одну строчку
N
# если загруженная строчка пустая, то мы
# выводим этот хвост и выходим(хвост короче 40), то-же происходит,
# если строк больше нет.
/\n$/ b
# команда N добавила нам \n, меняем его на пробел
s/\n/ /
# продолжаем цикл
b begin_loop
}
# в строке больше 40 символов - отрезаем хвост
# тут используется жадность квантификатора - он стремится захватить как
# можно более длинную строку
s/^(.{,40})\s/\1\n/
# если хвост отрезан, (точнее отделён \n), тогда переходим к печати.
t end_loop
# особый случай - строка длиннее 40 символов, однако разрезать её нельзя.
# попробуем отрезать хоть как-то, пускай у нас иные строчки будут слишком
# длинные, но только те, которые никак не порезать
s/^(\S+)\s/\1\n/

:end_loop
# печать. печатается только отрезанная строка до первого \n
P
# следующая команда вырезает только-что распечатанную строку,
# и переходит к метке begin_loop, без загрузки сл. строки
D

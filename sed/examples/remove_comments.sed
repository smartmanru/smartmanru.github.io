#!/bin/sed -rf

# сохранение строки
h

:begin_loop
	# p
	# удаление обычного начала
	s/^[^'"#\]+(.*)/\1/
	t begin_loop
	# удаление экранированных символов
	s/^\\.//
	t begin_loop
	# удаление закавыченного блока
	s/^'[^']*'//
	t begin_loop
	s/^'.*//
	T lq
		# незакрытый блок кавычек, необходима подгрузка
		${
			x
			b end
		}
		n
		H
		s/^/'/
		t begin_loop		
	:lq
	# удаление экранированной кавычки внутри закавыченного блока
	s/^"[^"]*\\"/"/
	t begin_loop
	# удаление закавыченного блока
	s/^"[^"]*"//
	t begin_loop
	s/^".*//
	T lq2
		${
			x
			b end
		}
		n
		H
		s/^/"/
		t begin_loop
	:lq2
	/^\#/{
		# теперь строка начинается с #
		H
		x
		s/^(.*)(\#[^\n]*)\n\2$/\1/
		b end
	}
	# а комента и нету
	x
:end
# s/.*/\x1b[32m'&'\x1b[0m/
